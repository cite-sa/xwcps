/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'yannis' at '12/3/15 3:47 PM' with Gradle 2.9
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.9/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'gr.cite.earthserver'
version = '0.1-SNAPSHOT'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

compileJava {
    sourceCompatibility  = '1.8'
    targetCompatibility  = '1.8'
}

repositories {
	mavenLocal()
    mavenCentral()
    maven {
    	url "http://maven.local.cite.gr:8080/repository/snapshots/"
    }
    maven {
    	url "http://maven.local.cite.gr:8080/repository/internal/"
    }
}

dependencies {
	antlr "org.antlr:antlr4:4.5"

    compile 'org.slf4j:slf4j-api:1.7.13',
    		'org.glassfish.jersey.core:jersey-client:2.22.1',
    		'org.glassfish.jersey.media:jersey-media-multipart:2.22.1',
    		
    		'commons-io:commons-io:2.4',
    		'com.google.guava:guava:19.0',
    		'org.antlr:antlr4:4.5',
    		
    		
    		// @maven.local.cite.gr
    		'gr.cite.scarabaeus:xml-utils:1.1.1',
    		
			'gr.cite.exmms:exmms-commons:0.1-SNAPSHOT'
    		

    runtime 'ch.qos.logback:logback-classic:1.1.3'

    testCompile 'junit:junit:4.12',
    			'org.mockito:mockito-core:1.10.19'
    			
    			
}


// ANTLR arguments
def grammarArguments = ["-visitor", "-long-messages"] 

generateGrammarSource {
    arguments +=  ["-package", "gr.cite.earthserver.wcps.grammar"] +  grammarArguments
    
    doLast {
	    copy {
			from 'build/generated-src/antlr/main/'
			include '*.*'
			into 'build/generated-src/antlr/main/gr/cite/earthserver/wcps/grammar'
	    }
	    project.delete fileTree('build/generated-src/antlr/main').include('*.*')
	}
}

generateTestGrammarSource {
	arguments += ["-package", "test.calculator"] + grammarArguments
}
